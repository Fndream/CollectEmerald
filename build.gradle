plugins {
    id 'maven-publish'
    id 'fabric-loom' version '1.14-SNAPSHOT' apply false

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version '48cb0c80f7'

    // https://github.com/Fallen-Breath/yamlang
    id 'me.fallenbreath.yamlang' version '1.5.0' apply false
}

version = project.mod_version
group = project.maven_group

preprocess {
    strictExtraMappings.set(false)

    def mc12108 = createNode("1.21.8", 1_21_08, "")
    def mc12111 = createNode("1.21.11", 1_21_11, "")

    mc12108.link(mc12111, file("versions/mapping_12108_12111.txt"))
}

// 收拢所有版本的jar文件
// 这个非常必要，项目构建时都应该用这个任务而非自带的build
// 当然，单独一个子项目的构建除外
tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy = DuplicatesStrategy.INCLUDE
            }
        }
    }
}